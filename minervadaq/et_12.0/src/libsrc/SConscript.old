# scons build file for libet and libet_remote
Import('env', 'archIncInstallDir', 'incInstallDir', 'libInstallDir', 'debugSuffix')
import os
from os import sep

headerList = ['et.h',
              'et_private.h',
              'et_network.h',
              'etCommonNetwork.h',
              'et_data.h']

linkList = [archIncInstallDir + sep + 'et.h',
            archIncInstallDir + sep + 'et_private.h',
            archIncInstallDir + sep + 'et_network.h',
            archIncInstallDir + sep + 'etCommonNetwork.h',
            archIncInstallDir + sep + 'et_data.h']

jniFileList = ['et_jni.c']

fileList = ['et_attachment.c',
            'et_bridge.c',
            'et_common.c',
            'et_data.c',
            'et_event.c',
            'et_init.c',
            'et_local.c',
            'et_mem.c',
            'et_network.c',
            'etCommonNetwork.c',
            'et_noshare.c',
            'et_openconfig.c',
            'et_readwrite.c',
            'et_remote.c',
            'et_server.c',
            'et_statconfig.c',
            'et_station.c',
            'et_sysconfig.c',
            'et_system.c']

remoteFileList = ['et_init.c',
                  'et_network.c',
                  'etCommonNetwork.c',
                  'et_openconfig.c',
                  'et_remote.c',
                  'et_remoteclient.c',
                  'et_statconfig.c',
                  'et_sysconfig.c']

env.AppendUnique(CPPPATH = ['.'])
etLib       = env.SharedLibrary(target = 'et'+debugSuffix,        source = fileList)
etRemoteLib = env.SharedLibrary(target = 'et_remote'+debugSuffix, source = remoteFileList)
jniLib      = env.SharedLibrary(target = 'et_jni'+debugSuffix,    source = jniFileList,
                                LIBS = ['et'+debugSuffix], LIBPATH = [libInstallDir])

env.Alias("install", env.Install(target = incInstallDir, source = headerList))
env.Alias("install", env.Install(target = libInstallDir, source = etLib))
env.Alias("install", env.Install(target = libInstallDir, source = etRemoteLib))
env.Alias("install", env.Install(target = libInstallDir, source = jniLib))
# create symbolic links to installed header files
env.Alias("install", env.CreateSymbolicLinks(target = linkList, source = env.Install(target = incInstallDir, source = headerList)))


